neuron adex_gamma_E:

    state:
        V_m mV = -65 mV      # Membrane potential
        w pA = 0 pA        # Spike-adaptation current
        r integer = 0 # Counts number of tick during the refractory period

        g_NMDA real = 0
        g_NMDA$ real = g_NMDA_const * (1 / tau_syn_rise_NMDA - 1 / tau_syn_decay_NMDA)

    equations:

        inline V_bounded mV = min(V_m, V_peak) # prevent exponential divergence

        inline B real  = 1/(1+exp(-0.062* V_bounded * 1/mV) *(Mg/3.57)) 

        kernel g_GABA = exp(-t/tau_decay_GABA) # inputs from the GABA conductance.
        kernel g_AMPA = exp(-t/tau_decay_AMPA) # inputs from the AMPA conductance
        kernel g_NMDA' = g_NMDA$ - g_NMDA / tau_syn_rise_NMDA,
               g_NMDA$' =-g_NMDA$ / tau_syn_decay_NMDA


        recordable inline I_AMPA pA = convolve(g_AMPA, AMPA) * nS * (V_bounded - E_e)
        recordable inline I_GABA pA = convolve(g_GABA, GABA) * nS * (V_bounded - E_i)
        recordable inline I_NMDA pA = convolve(g_NMDA, NMDA) * nS * (V_bounded - E_e) * B

        inline I_syn pA = I_AMPA + I_NMDA + I_GABA

        V_m' = (-g_L * (V_bounded - E_L) + g_L * Delta_T * exp((V_bounded-V_th)/Delta_T) - w - I_syn + I_e + I_stim) / C_m
        w' = (a * (V_bounded - E_L) - w) / tau_w

#parameters 
    parameters:
        # membrane parameters
        C_m pF = 150.0 pF         # Membrane Capacitance
        t_ref ms = 5.0 ms         # Refractory period
        V_reset mV = -65.0 mV     # Reset Potential
        g_L nS = 10.0 nS          # Leak Conductance
        E_L mV = -65.0 mV         # Leak reversal Potential (aka resting potential)

        # spike adaptation parameters
        a nS = 4 nS               # Subthreshold adaptation
        b pA = 20 pA            # Spike-triggered adaptation
        Delta_T mV = 2.0 mV       # Slope factor
        tau_w ms = 500 ms       # Adaptation time constant
        V_th mV = -40 mV        # Threshold Potential
        V_peak mV = 30 mV       # Spike detection threshold

        #Reversal potential of synapses

        E_e mV = 0 mV
        E_i mV = -80 mV

        # Time decay constants
        tau_decay_AMPA ms = 1.5 ms
        tau_decay_GABA ms = 7.5 ms

        #NMDA paramters

        Mg real = 1 
        tau_syn_rise_NMDA ms = 2 ms    # Synaptic time constant excitatory synapse
        tau_syn_decay_NMDA ms = 200 ms

        # constant external input current
        I_e pA = 0 pA

    internals:

        t_peak_NMDA real = tau_syn_decay_NMDA * tau_syn_rise_NMDA * ln(tau_syn_decay_NMDA / tau_syn_rise_NMDA) / (tau_syn_decay_NMDA - tau_syn_rise_NMDA)
        g_NMDA_const real = 1 / (exp(-t_peak_NMDA / tau_syn_decay_NMDA) - exp(-t_peak_NMDA / tau_syn_rise_NMDA))

        # refractory time in steps
        
        RefractoryCounts integer = steps(t_ref)

    input:
        AMPA <- spike
        NMDA <- spike
        GABA <- spike
        I_stim pA <- continuous

    output:
        spike

    update:
        integrate_odes()

        if r > 0: # refractory
            r -= 1 # decrement refractory ticks count
            V_m = V_reset # clamp potential
        elif V_m >= V_peak: # threshold crossing detection
            r = RefractoryCounts
            V_m = V_reset # clamp potential
            w += b
            emit_spike()
